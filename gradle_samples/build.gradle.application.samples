buildscript {
    repositories {
        mavenCentral() }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.1.3'
    }
}

apply plugin: 'com.android.application'

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.1"

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }
    }


    signingConfigs {
        release {
            storeFile file(STORE_FILE)
            storePassword STORE_PASSWORD
            keyAlias KEY_ALIAS
            keyPassword KEY_PASSWORD
        }
    }

    defaultConfig {
        //applicationId "org.eazegraph.app"
        minSdkVersion 9
        targetSdkVersion 21
        versionCode 1
        versionName "1.0"
    }

    lintOptions {
        abortOnError false
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            signingConfig signingConfigs.release

            // this is used to alter output directory and file name. If you don't need it
            // you can safely comment it out.
            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def file = output.outputFile

                    String parent = file.parent
                    if (project.hasProperty('OUTPUT_DIR') && new File((String) OUTPUT_DIR).exists())
                        parent = OUTPUT_DIR

                    output.outputFile = new File(
                            parent,
                            (String) file.name.replace(
                                    ".apk",

                                    // alter this string to change output file name
                                    "-" + defaultConfig.versionName + "-"+ APP_NAME + "-" + defaultConfig.versionCode + ".apk"
                            )
                    )
                }
            }
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
}


//**********************note************************
//save next properties to gradle.properties file

//STORE_FILE=/work/eagle.keystore
//STORE_PASSWORD=eagle
//KEY_ALIAS=eagle
//KEY_PASSWORD=eagle
//APP_NAME=app_name
